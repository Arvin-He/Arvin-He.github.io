---
title: NSIS 打包 windows可执行文件
date: 2017-05-13 08:49:17
tags: [NSIS]
categories: 编程
---
### NSIS简介与特性
NSIS(Nullsoft Scriptable Install System)是一种允许程序员为Windows创建此类安装程序的工具。它是根据开源许可证发布的，完全免费使用。NSIS创建能够安装，卸载，设置系统设置，提取文件等的安装程序。由于它基于脚本文件，您可以完全控制安装程序的每个部分。脚本语言支持变量，函数和字符串操作，就像一般的编程语言一样，但是设计用于创建安装程序。即使具有所有这些功能，NSIS仍然是最小的安装程序系统。使用默认选项，它的开销只有34 KB。

NSIS将所有文件和安装脚本编译成一个可执行文件，以便您的应用程序易于分发。

可以创建与Windows 95,98，ME，NT4,2000，XP，2003，Vista，Server 2008,7，Server 2008R2,8，Server 2012，8.1，Server 2012R2和Windows 10兼容的单个安装程序.

可以选择三种不同的集成压缩方法（ZLib，BZip2，LZMA）。 LZMA压缩比任何其他常见的压缩方法提供更好的结果。不必使用大型自解压缩存档模块或其他应用程序，压缩支持包含在34 KB开销中。

与只能根据文件和注册表项列表生成安装程序的其他系统不同，NSIS具有强大的脚本语言。此脚本语言是为安装程序设计的，并具有帮助您执行许多安装任务的命令。您可以轻松添加自定义逻辑并处理不同的升级，版本检查和更多。

脚本语言提供可以在目标系统上使用的命令。从文件夹创建和注册表编辑到文本/二进制文件修改，修改环境变量和系统重新引导等简单功能。通过使用所提供的插件之一，甚至可以直接调用Windows API。

可以创建自定义向导页面以获取用户输入或集成配置选项。 NSIS包括一个经典和现代的向导界面，甚至可以创建自己的自定义界面（对话框，字体，背景，图标，文本，复选标记，图像等）。

NSIS编译器具有强大的预处理器。允许将多个项目集成到单个安装程序中或自动生成安装程序构建。还可以生成不同的版本，如精简版和完整版本。

完全多语言，在一个安装程序中支持多种语言。有60多个翻译可用，但您也可以创建自己的翻译。 Unicode支持允许更多的语言。 

Installer 使用CRC32校验和进行自检 压缩数据大小的开销较小 

能够以文本或RTF格式显示许可协议 能够从注册表检测目标目录 

易于使用的插件系统（包括用于创建自定义对话框，互联网连接，HTTP下载，文件修补，Win32 API调用等的插件） 

Installer 可以大到2GB 

自动安装的可选静音模式 一个预处理器，支持定义的符号，宏，条件编译，标准预定义 

具有PHP和汇编元素的编码体验（包括用户变量，堆栈，实际流量控制等） 

### 脚本文件
要创建一个NSIS安装程序，首先必须编写一个NSIS脚本。 NSIS脚本只是一个具有特殊语法的常规文本文件。可使用任何文本编辑器编辑脚本。建议使用显示行号的文本编辑器，因为NSIS使用行号来指示错误的位置，并警告您错误可能在哪里。还建议支持语法高亮的编辑器。可以下载专为NSIS编辑的编辑器和NSIS Wiki中的语法突出显示文件。

在NSIS脚本中，每一行都被视为一个命令。如果命令太长，则可以在行尾使用反斜杠'\'。编译器会将新行作为上一行的补充例如：
```
Messagebox MB_OK|MB_ICONINFORMATION \
"This is a sample that shows how to use line breaks for larger commands in NSIS scripts"
```

如果要在字符串中使用双引号，您可以使用$\"来转义引号，也可以使用不同类型的引号（如`或'）引用字符串。

脚本文件的默认扩展名为.nsi。头文件具有.nsh扩展名。头文件可以帮助您将脚本分成多个代码块，您还可以将头文件中的函数或宏放在多个安装程序中。这使得更新更容易，它也使您的脚本更容易阅读。在脚本中包含头文件使用`！include`。驻留在您的NSIS目录下的Include目录中的头文件可以仅包含其名称。例如：`!include Sections.nsh`

### 脚本结构
NSIS脚本包含安装程序属性，页面和部分/功能。您还可以使用编译器命令进行编译时操作。 OutFile指令是必需的，并告诉NSIS哪里可以编写安装程序，还需要至少一个部分。

#### 安装程序属性
安装程序属性确定安装程序的行为和外观。通过这些属性，您可以更改安装过程中显示的文本，安装类型数量等。大多数命令只能在运行时被设置并且不可更改。其他基本说明是 Name 和 InstallDir.

#### Pages
非静默安装程序有一组向导页面，让用户配置安装程序。您可以使用Page命令（或更多高级设置的PageEx）设置要显示的页面。典型的一组页面如下所示：
```
Page license
Page components
Page directory
Page instfiles
UninstPage uninstConfirm
UninstPage instfiles
```
对于安装程序，此典型的一组页面将显示许可协议，允许选择要安装的组件，允许选择安装目录，最后在instfiles页面中安装所选组件。对于卸载程序，它将显示一个确认页面，并在instfiles页面中卸载。

#### Sections
安装程序通常有几个用户可以安装的东西。例如，在NSIS分发安装程序中，您可以选择安装其他工具，插件，示例等。这些组件中的每一个都有自己的代码段。如果用户选择安装此组件，则安装程序将执行该代码。在脚本中，该代码在节中定义。每个部分对应于组件页面上的一个组件。该部分的名称是显示的组件名称，如果选择该组件，则将执行部分代码。只需一个部分即可构建安装程序，但如果要使用组件页面并让用户选择要安装的部件，则必须使用多个部分。

卸载程序也可以有多个部分。卸载程序的名称前缀为“un”。例如：
```
Section "Installer Section"
SectionEnd

Section "un.Uninstaller Section"
SectionEnd
```
在sections中使用的指令与安装程序属性说明不同，它们在运行时在用户的计算机上执行。这些指令可以提取文件，读取和写入注册表，INI文件或普通文件，创建目录，创建快捷方式等等。最基本的说明是SetOutPath，它告诉安装程序在哪里提取文件，File提取文件。
```
Section "My Program"
  SetOutPath $INSTDIR
  File "My Program.exe"
  File "Readme.txt"
SectionEnd
```
#### Functions
函数可以包含脚本代码，就像sections。section和function之间的差异是它们被调用的方式。有两种类型的function，用户function和回调function。
用户函数由用户使用call指令从section或其他函数中调用,否则用户功能将不会执行。执行该函数的代码后，安装程序将继续执行“call”指令后面的指令，除非你在函数中中止安装。如果您有一组需要在安装程序中的几个位置执行的指令，用户函数将非常有用。如果您将代码放入函数中，您可以节省拷贝时间，且更容易维护代码。

回调函数由安装程序在某些定义的事件（如安装程序启动时）调用。回调是可选的。如果你想让用户参加到你的安装程序中，你可以定义一个名为.onInit的函数。 NSIS编译器将通过名称将此函数识别为回调函数，并在安装程序启动时调用它。
```
Function .onInit
  MessageBox MB_YESNO "This will install My Program. Do you wish to continue?" IDYES gogogo
    Abort
  gogogo:
FunctionEnd
```
中止在回调函数中有特殊的含义。每个回调函数都有其自己的含义，在上述示例中，Abort告诉安装程序停止初始化安装程序并立即退出。

### 使用脚本
#### 逻辑代码结构
可以使用StrCmp，IntCmp，IfErrors，Goto等来有条件地执行代码或执行循环中的代码。但是，这样做更容易一些。 LogicLib提供了一些非常简单的宏，可以轻松构建复杂的逻辑结构。
例如，检查没有LogicLib的变量的值
```
StrCmp $0 'some value' 0 +3
  MessageBox MB_OK '$$0 is some value'
  Goto done
StrCmp $0 'some other value' 0 +3
  MessageBox MB_OK '$$0 is some other value'
  Goto done
# else
  MessageBox MB_OK '$$0 is "$0"'
done:
```
然而，使用LogicLib，代码可读性更好，易于理解，如下例所示。
```
${If} $0 == 'some value'
  MessageBox MB_OK '$$0 is some value'
${ElseIf} $0 == 'some other value'
  MessageBox MB_OK '$$0 is some other value'
${Else}
  MessageBox MB_OK '$$0 is "$0"'
${EndIf}
```
也可以使用开关完成相同的操作，如下例所示。
```
${Switch} $0
  ${Case} 'some value'
    MessageBox MB_OK '$$0 is some value'
    ${Break}
  ${Case} 'some other value'
    MessageBox MB_OK '$$0 is some other value'
    ${Break}
  ${Default}
    MessageBox MB_OK '$$0 is "$0"'
    ${Break}
${EndSwitch}
```
还支持多个条件。以下示例将通知用户，如果$ 0和$ 1均为空。
```
${If} $0 == ''
${AndIf} $1 == ''
  MessageBox MB_OK|MB_ICONSTOP 'both are empty!'
${EndIf}
```
LogicLib消除了对标签和相对跳转的需要，从而防止标签名称冲突，并且消除了在每次脚本更改时手动调整相对跳转偏移量的需要
它还通过支持common，do和for循环来简化循环。以下所有示例使用LogicLib计数为5。
```
StrCpy $R1 0
${While} $R1 < 5
  IntOp $R1 $R1 + 1
  DetailPrint $R1
${EndWhile}
${For} $R1 1 5
  DetailPrint $R1
${Next}
StrCpy $R1 0
${Do}
  IntOp $R1 $R1 + 1
  DetailPrint $R1
${LoopUntil} $R1 >= 5
```
要使用LogicLib，需要在脚本顶部附近添加以下行。`!include LogicLib.nsh`

### 变量
您可以使用Var命令声明自己的变量（$ VARNAME）。变量是全局的，可以在任何部分或功能中使用。
声明和使用用户自定义变量：
```
Var BLA ;Declare the variable

Section bla

  StrCpy $BLA "123" ;Now you can use the variable $BLA

SectionEnd
```
另外还有一个堆栈，也可以用于临时存储。访问堆栈使用命令Push和Pop。 Push为堆栈添加一个值，Pop删除一个值并设置变量。
对于共享代码，有20个可用的寄存器（如$ 0和$ R0）。这些静态变量不必被声明，您不会得到任何名称冲突。如果要在共享代码中使用这些变量，请将原始值存储在堆栈中，然后恢复原始值。

### debug脚本
与NSIS工作越多，脚本就越复杂。这将增加错误的几率，特别是在处理很多变量时。有几种方法可以帮助调试代码。要显示变量的内容，应该使用MessageBox或DetailPrint。要获得有关所有变量的简要概述，应该使用插件DumpState。默认情况下，安装程序的所有操作都将打印在日志窗口中。如果您在日志窗口中右键单击并选择“将详细信息复制到剪贴板”，则可以访问该日志。还有一种方法直接写入文件.

### 执行脚本
当用户运行安装程序或卸载程序时，按照脚本中定义的顺序显示页面。当达到instfiles页面时，与所选组件对应的section按照脚本中定义的顺序执行。如果组件页面未显示，则所有section都将被执行，假设它们未被选中或由脚本禁用。

除了代码中，还有回调函数中的代码。如果定义了它们，则可能在这些部分代码之前执行它们。例如，.onIn回调函数在脚本中的任何其他操作之前执行。还有页面回调函数在页面显示过程的某些点执行。

### 编译器命令
编译器命令将在您的计算机上的编译时执行。它们可用于条件编译，包括头文件，执行应用程序，更改工作目录等。最常见的用法是定义。定义是编译时常数。您可以定义产品的版本号，并在脚本中使用。例如：
```
!define VERSION "1.0.3"
Name "My Program ${VERSION}"
OutFile "My Program Installer - ${VERSION}.exe"
```
另一个常见的用法是宏。宏用于在编译时插入代码，具体取决于定义和使用定义的值。宏的命令在编译时被插入。这使您只能编写代码一次，并多次使用。例如：
```
!macro MyFunc UN
Function ${UN}MyFunc
  Call ${UN}DoRegStuff
  ReadRegStr $0 HKLM Software\MyProgram key
  DetailPrint $0
FunctionEnd
!macroend

!insertmacro MyFunc ""
!insertmacro MyFunc "un."
```
此宏可帮助您避免为安装程序和卸载程序编写相同的代码。两个！insertmacros插入两个函数，一个用于安装程序，名为MyFunc，另一个用于卸载程序，名为un.MyFunc，它们都完全相同。

### 编译器
创建脚本之后，为了创建安装程序，您需要做的第二件事就是编译脚本。 MakeNSIS.exe是NSIS编译器。它读取您的脚本，解析它并为您创建一个安装程序。

要编译，可以右键单击.nsi文件，然后选择编译NSIS脚本。这将导致MakeSISW（NSIS编译器接口）启动并调用MakeNSIS来编译脚本。 MakeNSISW接收到MakeNSIS的输出，并在您可以看到它的窗口中显示，并将其复制，测试安装程序等等。也可以从命令提示符使用makensis.exe。

编译器将检查您的脚本，并给您警告或错误。如果发生错误（即需要2个参数，但只有1个参数），则编译器将中止，并显示包括行号的短错误消息。对于非严重错误，编译器将发出警告（即，一个脚本中的两个DirText命令）。如果您的脚本没有错误，编译器将输出一个安装程序供您分发。

NSIS支持不同的压缩方法，如这里所述。 ZLIB是默认的压缩方法，它是快速的，只使用一点点的内存。 LZMA是创建互联网分发小型安装程序的好方法。 BZIP2通常压缩比ZLIB好，但不如LZMA那样好，如果你需要更少的内存使用或快速的脚本编译，这是很有用的。

它也可以在Linux，BSD或Mac OS X服务器上编译Windows安装程序。

### Modern UI
NSIS的流行用户界面是现代用户界面。它有一个界面，如最近的Windows版本的向导。现代UI不仅是一个定制的资源文件，它具有很多新的界面元素。它具有一个白色标题来描述当前步骤，组件页面上的描述区域，欢迎页面，允许用户运行应用程序或重启系统等的完成页面。

### 插件
NSIS支持可以从脚本调用的插件。插件是用C，C ++，Delphi或其他编程语言编写的DLL文件，因此为NSIS提供了更强大的代码库。
插件调用如下所示：
```
DLLName::FunctionName "parameter number 1" "parameter number 2" "parameter number 3"
```
每个插件的功能都有自己的要求，当涉及到参数时，有些将不需要，有些将接受尽可能多的参数。例子：
```
nsExec::ExecToLog '"${NSISDIR}\makensis.exe" /CMDHELP'
Pop $0 ; Process exit code or "error"
InstallOptions::dialog "$PLUGINSDIR\test.ini"
Pop $0 ; success/back/cancel/error
NSISdl::download http://download.nullsoft.com/winamp/client/winamp291_lite.exe $R0
Pop $0 ; "success" or a error code
```
NSIS知道的插件列在编译器输出的顶部（详细级别4）。 NSIS在NSIS目录下的Plugins文件夹中搜索插件，并列出所有可用的功能。您可以使用！addplugindir来告诉NSIS在其他目录中搜索。

NSIS发行版已经包含许多插件。 InstallOptions是一种流行的插件，可以与NSIS Page命令（请参阅页面）一起创建自定义页面。 Startmenu插件提供了一个页面，允许用户选择一个开始菜单文件夹。有很多插件用于不同的目的，请查看文档文件夹中的帮助文件和示例。您可以在线查找其他插件.

您也可以自己创建插件。 C / C ++和Delphi头文件已经可用，请参阅示例插件，了解如何执行此操作。包含插件的源代码也可以在源代码包中找到。

### 命令行用法
#### MakeNSIS用法
NSIS安装程序是通过使用“MakeNSIS”程序将NSIS脚本（.NSI）编译为安装程序可执行文件生成的。 NSIS开发套件安装程序设置您的计算机，以便您可以通过在资源管理器中右键单击并选择“编译”来编译.nsi文件。
如果要在命令行中使用MakeNSIS，makensis的语法是:
`makensis [ option | script.nsi | - ] [...]`

/ LICENSE显示许可证信息。 
/ V开关后跟数字在0和4之间将相应地设置输出的冗长度。 0 =无输出，1 =仅错误，2 =警告和错误，3 =信息，警告和错误，4 =全部输出。 
/ P开关后跟数字在0和5之间将相应地设置编译器进程的优先级。 0 =空闲，1 =低于正常，2 =正常（默认），3 =正常以上，4 =高，5 =实时。 
/ O开关后跟一个文件名，告诉编译器将其日志打印到该文件（而不是屏幕） 
/ PAUSE使makensis在退出之前暂停，这在从Windows直接执行时很有用。 
/ NOCONFIG禁止包含nsisconf.nsh。没有此参数，安装程序默认设置从nsisconf.nsh设置。 
/ CMDHELP打印命令（如果指定）或所有命令（如果未指定命令）的基本使用信息。 
/ HDRINFO打印有关哪些选项用于编译makensis的信息。 
/ NOCD将当前目录更改禁用.nsi文件的更改 
/ INPUTCHARSET允许您为没有BOM的文件指定特定的代码页。 （ACP | OEM | CP＃| UTF8 | UTF16 <LE | BE>） 
/ OUTPUTCHARSET允许您指定输出重定向时由stdout使用的代码页。 （ACP | OEM | CP＃| UTF8 [SIG] | UTF16 <LE | BE> [BOM]） 
/ PPO或/ SAFEPPO只会运行预处理器并将结果打印到stdout。安全版本不会执行如appendfile或！system这样的指令。 ！packhdr和！finalize从不执行。 
/ WX将警告视为错误 
使用/ D开关一次或多次会将符号添加到全局定义的列表（请参阅！define）。 
使用/ X开关一次或多次将执行您指定的代码。示例：“/ XAutoCloseWindow false” 为脚本名称指定破折号（ - ）会告诉makensis使用标准输入作为源。

注意:参数按顺序处理。 makensis/Ddef script.nsi 与 makensis script.nsi / Ddef 不一样。 如果指定了多个脚本，则将它们视为一个连接脚本。

### 环境变量
makensis检查一些环境变量，告诉它在何处找到需要的东西，以创建安装程序。这些变量包括：
NSISDIR，NSISCONFDIR - 安装了nsis数据和配置文件的位置。 NSISDIR更改脚本变量$ {NSISDIR}。
APPDATA（在Windows上）或HOME（在其他平台上） - 每个用户配置文件的位置。
```
Basic usage:

makensis.exe myscript.nsi
Quiet mode:

makensis.exe /V1 myscript.nsi
Force compressor:

makensis.exe /X"SetCompressor /FINAL lzma" myscript.nsi
Change script behavior:

makensis.exe /DUSE_UPX /DVERSION=1.337 /DNO_IMAGES myscript.nsi
Parameters order:

makensis /XSection sectioncontents.nsi /XSectionEnd
```
### 安装程序使用
生成的安装程序和卸载程序在命令行中接受几个选项。这些选项可以让用户对安装过程有更多的控制。
#### 常用选项
/ NCRC禁用CRC校验，除非在脚本中使用了CRCCheck force。 
/ S静默运行安装程序或卸载程序。 
/ D设置默认安装目录（$ INSTDIR），覆盖InstallDir和InstallDirRegKey。它必须是命令行中使用的最后一个参数，并且不能包含任何引号，即使路径包含空格。只支持绝对路径。
#### 卸载程序的特定选项
_？=设置$ INSTDIR。它也会阻止卸载程序将其自身复制到临时目录并从那里运行。它可以与ExecWait一起使用，等待卸载程序完成。它必须是命令行中使用的最后一个参数，并且不能包含任何引号，即使路径包含空格。
```
installer.exe /NCRC
installer.exe /S
installer.exe /D=C:\Program Files\NSIS
installer.exe /NCRC /S /D=C:\Program Files\NSIS
uninstaller.exe /S _?=C:\Program Files\NSIS
# uninstall old version
ExecWait '"$INSTDIR\uninstaller.exe" /S _?=$INSTDIR'
```

### 模板结构
预设参数（包括外部压缩器选择、编译选项、宏定义以及文件包含等） 
普通安装设置 
自定义函数 
安装程序区域内容 
安装程序回调函数及其相关函数定义 
卸载程序区域内容 
卸载程序回调函数及其相关函数定义 
```
; 以下脚本用以生成 study.exe 文件
; 它包含有授权协议
;
; 安装时若发现应用程序正在运行，安装程序会提示并替用户关闭应用程序
; 安装成功后会自动运行应用程序
;
; 启用新的 Modern UI 的宏指令语法，原先的部分指令将被禁止以防止重复
; 我会尽量展示如何基本指令的用法。当然宏指令也会与一些基本指令发生重
; 复定义的情况，不过 NSIS 照样能编译成功.
; 其中的一些路径、文件你可以根据需要更改

; 启用外部压缩工具（这里我们不用，原因看第 10 步）
;!define HAVE_UPX

; 若启用了外部压缩工具，这进行可执行文件头的压缩
!ifdef HAVE_UPX
; 用 UPX 压缩可执行文件头（用其他的相关工具也行）
!packhdr tmp.dat "E:UPX\upx --best tmp.dat"
!endif

; 编译选项

; 设置覆盖标记
SetOverwrite on
; 设置压缩选项
SetCompress auto
; 选择压缩方式
SetCompressor bzip2
; 设置数据块优化
SetDatablockOptimize on
; 设置数据写入时间
SetDateSave on

; 相关数据预设

; 软件的名字，后面的${NAME}调用这个名字
!define NAME "NSIS"
!define NAME_FULL "全方位掌握 NSIS 的使用"
!verbose 3
; 包含窗口消息定义文件
!include "${NSISDIR}\Examples\WinMessages.nsh"
; 包含新界面的宏指令文件
!include "${NSISDIR}\Examples\Modern UI\ModernUI.nsh"
; 包含标牌位图宏指令文件
!include "${NSISDIR}\Examples\branding.nsh"
!verbose 4
!define CURRENTPAGE $9
!define TEMP1 $R0
!define TEMP2 $R1

; 插入安装界面宏定义（这里我禁用了，启用也没问题。编译照样通过。后面我都定义过了）
;!insertmacro MUI_INTERFACE "modern2.exe" "adni18-installer-C-no48xp.ico" "adni18-uninstall-C-no48xp.ico" "modern.bmp" "smooth"

; 安装程序设置

; 输出文件
OutFile "F:\study.exe"
; 安装程序名称
Name /LANG=2052 "NSIS"
; 替换对话框样式
ChangeUI all "${NSISDIR}\Contrib\UIs\modern2.exe"
; 启用 WindowsXP 的视觉样式
XPstyle on
;设置字体
SetFont 宋体 9
; 标题名称
Caption /LANG=2052 "全方位掌握 NSIS 的使用"
; 标牌的内容
BrandingText /LANG=2052 /TRIMCENTER "Shao Hao"
; 安装程序图标
Icon "F:\12\QCDIcon.ico"
; 安装程序显示标题
WindowIcon on
; 添加标牌位图（放在顶部用 TOP 参数。后面接高度）
AddBrandingImage LEFT 105
; 设定渐变背景
BGGradient off
; 设置安静安装模式
;SilentInstall normal
; 设置安静卸载模式
;SilentUnInstall normal
; 执行 CRC 检查（最好用 on 打开。关闭是因为还要修改安装程序，看第 10 步）
CRCCheck off
; 加载相应的语言文件
LoadLanguageFile "${NSISDIR}\Contrib\Language files\SimpChinese.nlf"
; 替换各个对话框标题的文字
SubCaption /LANG=2052 0 "授权协议"
SubCaption /LANG=2052 1 "安装选项"
SubCaption /LANG=2052 2 "安装目录"
SubCaption /LANG=2052 3 "正在安装"
SubCaption /LANG=2052 4 "完成安装"
; 替换默认按钮的文字
MiscButtonText /LANG=2052 "< 上一步" "下一步 >" "取消" "关闭"
; 替换“安装”按钮的文字
;InstallButtonText /LANG=2052 "安装"
; 替换当文件无法被写入时的警告窗的文字
FileErrorText /LANG=2052 "无法写入文件$\r$\n$0$\r$\n请确认文件属性不是只读且未被使用中！"

; 默认的安装目录
InstallDir "$PROGRAMFILES\QCD 3"
; 如果可能的化从注册表中监测安装路径
InstallDirRegKey HKLM \
"Software\Microsoft\Windows\CurrentVersion\Uninstall\NSIS" \
"UninstallString"

; 授权协议页面
LicenseText /LANG=2052 "NSIS 是免费软件。安装前请先阅读以下协议条款" "我同意"
; 使用授权协议文本
LicenseData /LANG=2052 "F:\12\License.txt"
; 设置授权协议页面的背景色
;LicenseBkColor 000000

; 选择要按安装的组件
ComponentText /LANG=2052 "现在将安装 ${NAME_FULL}到您的计算机：" "请选择安装类型" "或选择您希望安装的组件"
InstType "完全安装(都在了)"
InstType "典型安装"
InstType /LANG=2052 /CUSTOMSTRING=自定义?
;InstType /LANG=2052 /NOCUSTOM
;InstType /COMPONENTSONLYONCUSTOM
CheckBitmap "${NSISDIR}\Contrib\Icons\modern.bmp"
; 替换显示磁盘空间信息的文字
SpaceTexts /LANG=2052 "所需空间：" "可用空间："

; 提示用户输入目录的文本
; DirShow hide
; 替换显示选择安装目录的文字
DirText /LANG=2052 "请选择 ${NAME} 的安装路径：" "选择目录用以安装 ${NAME}：" "浏览..."
; 是否允许安装在根目录下
AllowRootDirInstall false

; 安装信息的颜色
;InstallColors 000000 FFFFFF
; 安装进度条显示方式
InstProgressFlags smooth colored
; 完成后自动关闭安装程序
AutoCloseWindow true
; 显示“显示详细细节”对话框
ShowInstDetails hide
; 替换“显示细节”按钮的文字
DetailsButtonText /LANG=2052 "显示细节"
; 替换“完成”按钮的文字
CompletedText /LANG=2052 "已完成"

; 卸载程序设置

; 替换卸载程序的文字
UninstallText /LANG=2052 "现在将从你的系统中卸载 ${NAME}：" "卸载目录："
; 卸载程序图标
UninstallIcon "F:\12\UnQCDIcon.ico"
; 替换卸载程序标题的文字
UninstallCaption /LANG=2052 "卸载 ${NAME}"
; 替换卸载程序每页按钮的文字
UninstallSubCaption /LANG=2052 0 "：确认"
UninstallSubCaption /LANG=2052 1 "：正在删除文件"
UninstallSubCaption /LANG=2052 2 "：完成"
; 卸载程序显示方式
ShowUninstDetails hide
; 替换“卸载”按钮的文字
UninstallButtonText /LANG=2052 "卸载"

; 安装程序内容及其相关的回调函数

; 安装程序内容
Section /e "!主要程序(必须安装)" SecCore
SectionIn 1 2 RO
; 设置输出路径，每次使用都会改变
SetOutPath $INSTDIR
; 循环包含目录下全部内容
File /r "F:\12\1\*.*"
; 只包含一个文件
File "F:\12\gf.gif"
; 处理动态连接库文件
IfFileExists "$INSTDIR\1.dll" 0 NoFile1
; 取消组件注册
UnRegDll "$INSTDIR\1.dll"
Delete "$INSTDIR\1.dll"
NoFile1:
File "F:\12\1.dll"
; 注册组件
RegDLL "$INSTDIR\QCDIconMgr.dll"
; 写入软件注册键值
WriteRegStr HKLM "Software\NSIS\NSIS" "" "$INSTDIR"
; 为 Windows 卸载程序写入键值
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\NSIS" "DisplayName" "NSIS（只用于移除）"
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\NSIS" "UninstallString" '"$INSTDIR\uninst.exe"'
; 写入生成卸载程序（可多次使用）
WriteUninstaller "$INSTDIR\uninst.exe"
SectionEnd

Section /e "扩展(可选)" SecUpdate
SectionIn 1
SetOutPath "$INSTDIR\Plugins"
File "F:\12\qcdplus\Plugins\2.dll"
File /r "F:\12\qcdplus\Plugins\3"
SectionEnd

SubSection /e "选项" SecOptions
Section /e "创建桌面图标" SecDesktopShortCut
SectionIn 1 2
SetOutPath "$INSTDIR\Plugins"
CreateShortCut "$DESKTOP\NSIS.lnk" "$INSTDIR\gf.gif"
WriteUninstaller "$INSTDIR\uninst.exe"
SectionEnd

Section /e "创建快速启动栏图标" SecQuickbarShortCut
SectionIn 1 2
SetOutPath "$INSTDIR\Plugins"
CreateShortCut "$QUICKLAUNCH\NSIS.lnk" "$INSTDIR\gf.gif"
WriteUninstaller "$INSTDIR\uninst.exe"
SectionEnd

Section /e "创建 NSIS 程序组" SecShortCutGroup
SectionIn 1 2
SetOutPath "$INSTDIR\Plugins"
CreateDirectory "$SMPROGRAMS\NSIS"
CreateShortCut "$SMPROGRAMS\QCD Player\gf.lnk" "$INSTDIR\gf.gif"
WriteUninstaller "$INSTDIR\uninst.exe"
SectionEnd
SubSectionEnd

Section "-Run"; 运行?
ExecWait '"$INSTDIR\2.exe" -p'
WriteINIStr "$INSTDIR\NSIS.ini" "2" "Language" "Chinese"
Exec "$INSTDIR\2.exe"
ExecShell open "$INSTDIR\汉化说明.htm"
SectionEnd

Section ""
;Invisible section to display the Finish header
!insertmacro MUI_FINISHHEADER SetHeader
SectionEnd

; 安装程序初始化设置
Function .onInit
; 发现有程序正在运行时提示退出后继续运行
loop:
FindWindow $R0 "NSIS"
IntCmp $R0 0 done
MessageBox MB_OKCANCEL \
"NSIS 正在运行。$\n点击“确定”按钮会关闭 NSIS 然后继续安装，点击“取消”按钮将退出安装程序" \
IDOK NoAbort
Abort
NoAbort:
SendMessage $R0 1029 0 0
; 稍等后继续检测直至检测不到或用户选择取消
Sleep 444
Goto loop
done:
; 显示 Logo 画面
SetOutPath $TEMP
File /oname=qcd_logo.bmp "F:\12\logo.bmp"
File /oname=magiclime.exe "${NSISDIR}\Bin\magiclime.exe"
ExecWait '"$TEMP\magiclime.exe" qcd_logo.bmp'
Delete "$TEMP\magiclime.exe"
Delete "$TEMP\qcd_logo.bmp"
; 初始化标牌位图
!insertmacro BI_INIT $R0
FunctionEnd

; 安装程序主界面的相关文本
Function SetHeader
!insertmacro MUI_HEADER_INIT
!insertmacro MUI_HEADER_START 1
!insertmacro MUI_HEADER_TEXT 2052 "授权协议" "安装 ${NAME} 之前，请认真阅读一下授权条款."
!insertmacro MUI_HEADER_STOP 1
!insertmacro MUI_HEADER_START 2
!insertmacro MUI_HEADER_TEXT 2052 "选择组件" "请选择你要安装的组件."
!insertmacro MUI_HEADER_STOP 2
!insertmacro MUI_HEADER_START 3
!insertmacro MUI_HEADER_TEXT 2052 "选择安装位置" "为 ${NAME} 选择一个安装目录."
!insertmacro MUI_HEADER_STOP 3
!insertmacro MUI_HEADER_START 4
!insertmacro MUI_HEADER_TEXT 2052 "安装" "${NAME} 已经安装，请等待."
!insertmacro MUI_HEADER_STOP 4
!insertmacro MUI_HEADER_START 5
!insertmacro MUI_HEADER_TEXT 2052 "完成" "安装顺利完成."
!insertmacro MUI_HEADER_STOP 5
!insertmacro MUI_HEADER_END
FunctionEnd

; 初始化安装程序对话框的显示文本
Function .onInitDialog
!insertmacro MUI_INNERDIALOG_INIT
!insertmacro MUI_INNERDIALOG_START 1
!insertmacro MUI_INNERDIALOG_TEXT 2052 1040 "如果你同意协议中的所有条款,选择“我同样”继续安装,如果你选择了“取消”,安装程序将终止,只有接受条款才能安装 ${NAME}."
!insertmacro MUI_INNERDIALOG_STOP 1
!insertmacro MUI_INNERDIALOG_START 2
!insertmacro MUI_INNERDIALOG_TEXT 2052 1042 "描述"
!insertmacro MUI_INNERDIALOG_TEXT 2052 1043 "移动你的鼠标到组件上,便可以见到相关的描述."
!insertmacro MUI_INNERDIALOG_STOP 2
!insertmacro MUI_INNERDIALOG_START 3
!insertmacro MUI_INNERDIALOG_TEXT 2052 1041 "目标文件夹"
!insertmacro MUI_INNERDIALOG_STOP 3
!insertmacro MUI_INNERDIALOG_END
FunctionEnd

; 转到下页面时的处理
Function .onNextPage
!insertmacro MUI_NEXTPAGE_OUTER
!insertmacro MUI_NEXTPAGE SetHeader
; 标牌位图设置
!insertmacro BI_NEXT
FunctionEnd

; 转到上一页面时的处理
Function .onPrevPage
!insertmacro MUI_PREVPAGE
; 标牌位图设置
!insertmacro BI_PREV
FunctionEnd

; 鼠标移到指定组件时的显示处理
Function .onMouseOverSection
; 用宏指令设置安装自己的注释文本
!insertmacro MUI_DESCRIPTION_INIT
!insertmacro MUI_DESCRIPTION_TEXT 2052 ${SecCore} "安装主应用程序"
!insertmacro MUI_DESCRIPTION_TEXT 2052 ${SecUpdate} "安装 NSIS 的更新或新增插件"
!insertmacro MUI_DESCRIPTION_TEXT 2052 ${SecOptions} "选择 NSIS 的其他安装选项，例如：创建快捷方式和应用程序组的"
!insertmacro MUI_DESCRIPTION_TEXT 2052 ${SecDesktopShortCut} "在用户的桌面上创建 NSIS 的快捷方式"
!insertmacro MUI_DESCRIPTION_TEXT 2052 ${SecQuickbarShortCut} "在用户的快速栏里创建 NSIS 快捷方式"
!insertmacro MUI_DESCRIPTION_TEXT 2052 ${SecShortCutGroup} "在用户的开始菜单的程序中创建 NSIS 的快捷方式组"
!insertmacro MUI_DESCRIPTION_END
FunctionEnd

; 安装成功完成后的设置
Function .onInstSuccess
; 自己加吧:)
FunctionEnd

; 放弃安装时警告窗口的显示信息
Function .onUserAbort
!insertmacro MUI_ABORTWARNING 2052 "你确定要推出 ${NAME} 的安装吗?"
!insertmacro MUI_ABORTWARNING_END
FunctionEnd

; 安装程序标牌位图列表
!insertmacro BI_LIST
!insertmacro BI_LIST_ADD "F:\12\Brand1.bmp" /RESIZETOFIT
!insertmacro BI_LIST_ADD "F:\12\Brand2.bmp" /RESIZETOFIT
!insertmacro BI_LIST_ADD "F:\12\Brand3.bmp" /RESIZETOFIT
!insertmacro BI_LIST_END

; 卸载程序及其相关回调函数

; 卸载程序内容
Section "Uninstall"
ExecWait '"$INSTDIR\12.exe" /un'
; 循环删除文件直至删除文件夹
RMDir /r "$SMPROGRAMS\NSIS"
UnRegDLL "$INSTDIR\2.dll"
Delete "$INSTDIR\QCDIconMgr.dll"
Delete "$INSTDIR\*.*"
DeleteRegKey HKLM "Software\NSIS"
DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\NSIS"
MessageBox MB_YESNO|MB_ICONQUESTION \
"是否连插件目录也要删除？$\n(若您要保留这些文件，请点击下面的“否”按钮)" \
IDNO NoDelete
; 全删光！！！
RMDir /r "$INSTDIR"
NoDelete:
SectionEnd

; 定义卸载程序标牌位图
!define BI_UNINSTALL
!include "${NSISDIR}\Examples\branding.nsh"

; 初始化卸载程序对话框的设置
Function un.onInit
; 初始化标牌位图
!insertmacro BI_INIT $R0
FunctionEnd

; 卸载程序主界面的相关文本
Function un.SetHeader
!insertmacro MUI_HEADER_INIT
!insertmacro MUI_HEADER_START 1
!insertmacro MUI_HEADER_TEXT 2052 "卸载 ${NAME}" "${NAME_FULL} 将从你的系统里移除."
!insertmacro MUI_HEADER_STOP 1
!insertmacro MUI_HEADER_START 2
!insertmacro MUI_HEADER_TEXT 2052 "卸载" "${NAME} 正在被卸载，请等待."
!insertmacro MUI_HEADER_STOP 2
!insertmacro MUI_HEADER_START 3
!insertmacro MUI_HEADER_TEXT 2052 "完成" "${NAME_FULL} 已从你的系统中移除."
!insertmacro MUI_HEADER_STOP 3
!insertmacro MUI_HEADER_END
FunctionEnd

; 初始化卸载程序对话框时的处理
Function un.onInitDialog
; 自己加:)
FunctionEnd

; 卸载程序转到下一页时的处理
Function un.onNextPage
!insertmacro MUI_NEXTPAGE_OUTER
!insertmacro MUI_NEXTPAGE un.SetHeader
; 标牌位图设置
!insertmacro BI_NEXT
FunctionEnd

; 卸载程序完成时的处理
Function un.onUninstSuccess
; 再加^_^
FunctionEnd

; 放弃卸载程序时的处理
Function un.onUserAbort
FunctionEnd

; 卸载程序标牌位图列表
!insertmacro BI_LIST
!insertmacro BI_LIST_ADD "F:\12\UnBrand1.bmp" /RESIZETOFIT
!insertmacro BI_LIST_ADD "F:\12\UnBrand2.bmp" /RESIZETOFIT
!insertmacro BI_LIST_END

; 结束
```


### 参考
- [http://nsis.sourceforge.net/Docs/Chapter1.html#intro-about](http://nsis.sourceforge.net/Docs/Chapter1.html#intro-about)
- [http://www.cnblogs.com/myall/p/3637759.html](http://www.cnblogs.com/myall/p/3637759.html)